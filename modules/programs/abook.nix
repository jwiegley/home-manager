{ config, lib, pkgs, ... }:

with lib;

let

  cfg = config.programs.abook;

in {
  options.programs.abook = {
    enable = mkEnableOption "Abook";

    extraConfig = mkOption {
      type = types.lines;
      default = "";
      example = ''
        #  Defining a new custom field
        # -----------------------------
        #
        field pager = Pager
        field address_lines = Address, list
        field birthday = Birthday, date

        #  Defining a view/tab
        # ---------------------
        #
        view CONTACT = name, email
        view ADDRESS = address_lines, city, state, zip, country
        view PHONE = phone, workphone, pager, mobile, fax
        view OTHER = url, birthday

        # Automatically save database on exit
        set autosave=true
      '';
      description = ''
        Extra lines added to <filename>$HOME/.config/abook/abookrc</filename>.
        Available configuration options are described in the abook repository:
        <link xlink:href="https://sourceforge.net/p/abook/git/ci/master/tree/sample.abookrc" />
      '';
    };
  };

  config = mkIf cfg.enable (mkMerge [
    { home.packages = [ pkgs.abook ]; }

    (mkIf (cfg.extraConfig != "") {
      xdg.configFile."abook/abookrc".text = ''
        # Generated by Home Manager.
        # See http://abook.sourceforge.net/

        ${cfg.extraConfig}
      '';
    })
  ]);
}
